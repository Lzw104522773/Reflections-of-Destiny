BACKUP ~weidu_external/backup/Reflections~
AUTHOR ~SubtleD~

/*

PLAN FOR PART 1:
1)
 - make shards of mirror of opposition, place them 
2)
 - make thalantyr dead
 - ...set GV in... Candlekeep crypt? Or in dialogue with Tethtoril?
 - ...if GV=1, remove Thalantyr
 - ...if GV=1, give new dialogue to Melicamp, explaining Thantyr's killing and offering to buy/sell stuff
3)
 - change Belt's dialogue after doppel attack - no teleport, dunno location
 - put block on entrance to maze
 - maybe move entrance to maze...?
4)
 - place korlasz' crypt
 - script messenger to tell you where it is; put some FF outside it
 - FF captain dialogue into Korlasz crypt
 - change korlasz dialogue(s) mentioning sarevok dead
 - end of korlasz takes you back to the surface - see EBG1
5)
 - report to Belt; script Entar to enter and accuse you
 - Belt give wardstone
6)
 - allow entrance to maze; script some FF to die near traps
 - maybe change sarevok fight?
 - no game end after sarevok; see EBG1
7)
 - write dialogues for after: people pissed there is no body, no way to confirm your story
 - have Entar say the assassin is in custody
 - BREAK POINT: if continue to SoD, have them tell you you can prove your self by helping the city against the crusade, it is more important than figuring out yet another doppelganger situation
 - OR:.......if jump to BG2, say in no uncertain terms that the crusade is not your concern, the military can handle it; you need to get to the bottom of this clone thing. They say if that's your choice, the FF will march north without you.
8)
 - if skip SoD, cut scene to cells, spawn clone in cell
 - put brightly glowing bracers on clone
 - dialogue with/about clone
 - script timestop, teleport, party member removal
 - continue dialogue, everyone leaves, lose all inventory
9)
 - fade out/in, run SoD escape sequence
 - ...change dialogues where needed

*/


BEGIN ~Reflections of Destiny Part 1: the Mirror Shard~
REQUIRE_PREDICATE (GAME_IS ~eet~) OR (GAME_INCLUDES ~sod~) ~ ~

//make mirror shards__________________________________________________________________
//
ACTION_IF !(FILE_EXISTS_IN_GAME ~D5SIMUL9.SPL~) BEGIN
//...add statchecks
//
  APPEND ~splprot.2da~ ~D5_109_BIT=n%TAB%109%TAB%-1%TAB%8~ UNLESS ~D5_109_BIT=n~

  COPY_EXISTING ~splprot.2da~ ~override~
    COUNT_2DA_COLS cols
    READ_2DA_ENTRIES_NOW rows cols
    FOR (row = 1; row < rows; ++row) BEGIN
      READ_2DA_ENTRY_FORMER rows row 0 ~stat~
      PATCH_IF (~%stat%~ STRING_EQUAL_CASE ~D5_109_BIT=n~) BEGIN
        SET 109_bit_equal = %row%
      END
    END
  BUT_ONLY

  COPY_EXISTING ~spin101.spl~ ~override/d5si8049.spl~
    SAY NAME1 ~~
    SAY UNIDENTIFIED_DESC ~~
    LPF ALTER_SPELL_HEADER INT_VAR target = 1 END
    LPF DELETE_EFFECT END
    LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 target = 2 opcode = 233 parameter1 = (8 << 8) parameter2 = (109 + (0x10000 * 1)) timing = 9 END
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 236 target = 2 parameter2 = 3 timing = 0 duration = 60 END
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 215 target = 2 parameter2 = 3 timing = 1 duration = 0 STR_VAR resource = ~SPPROIMG~ END
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 2 timing = 4 duration = 2 STR_VAR resource = ~d5si8049~ END
  BUT_ONLY

  COPY_EXISTING ~simulacr.spl~ ~override~
    LPF ADD_SPELL_EFFECT INT_VAR target = 2 opcode = 326 parameter1 = (8 << 8) parameter2 = %109_bit_equal% timing = 1 STR_VAR resource = ~d5simul9~ END
  IF_EXISTS BUT_ONLY

  COPY_EXISTING ~spin101.spl~ ~override/d5simul9.spl~
    SAY NAME1 ~~
    SAY UNIDENTIFIED_DESC ~~
    LPF DELETE_EFFECT END
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 337 target = 2 parameter1 = 241 parameter2 = 101 timing = 1 END
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 72 target = 2 parameter1 = 4 parameter2 = 0 timing = 1 END
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 241 target = 2 parameter1 = 4 parameter2 = 1002 timing = 0 Duration = 1 END
  BUT_ONLY

END

COPY ~%MOD_FOLDER%/data/d5mshard.bam~ ~override~

COPY ~%MOD_FOLDER%/data/d5shard3.itm~ ~override~
	SAY NAME1 ~Mirror Shard~
	SAY UNIDENTIFIED_DESC ~Mirror Shard

This shard is a piece of an immensely powerful Mirror of Opposition, now broken. Even this small shard still carries some of the item's magic. If it is brandished at a creature and it captures their likeness, it can create a copy of that creature which is loyal to the holder of the Shard. The shard is quite fragile, however, and seems likely to break if used more than once or twice.
~

COPY ~%MOD_FOLDER%/data/d5shard1.itm~ ~override~
	SAY NAME1 ~Mirror Shard~
	SAY UNIDENTIFIED_DESC ~Mirror Shard

This shard is a piece of an immensely powerful Mirror of Opposition, now broken. Even this small shard still carries some of the item's magic. If it is brandished at a creature and it captures their likeness, it can create a copy of that creature which is loyal to the holder of the Shard. The shard is quite fragile, however, and seems likely to break if used more than once or twice.
~

ACTION_IF (FILE_EXISTS_IN_GAME ~deathk.cre~) BEGIN
  COPY_EXISTING ~deathk.cre~ ~override~
	ADD_CRE_ITEM ~d5shard3~ #3 #0 #0 ~IDENTIFIED~ ~INV~
END

// place other on the demon knight in chateau irenicus - "cambion" idemon01.cre
// ...and set the scripts and stuff for the shard's activation

ACTION_IF (FILE_EXISTS_IN_GAME ~idemon02.cre~) BEGIN

<<<<<<<< d5/d5irecam.baf
IF
	See("D5MICAMB")
THEN
	RESPONSE #100
		Attack("D5MICAMB")
END
>>>>>>>>
COPY ~d5/d5irecam.baf~ ~weidu_external/reflections/compile/d5irecam.baf~
COMPILE ~weidu_external/reflections/compile/d5irecam.baf~

<<<<<<<< d5/d5mircam.baf
IF
	Die()
THEN
	RESPONSE #100
		ReallyForceSpellDead(Myself,FIREBALL_DEATH)
END

IF
	Dead("D5CAMBIO")
THEN
	RESPONSE #100
		DestroySelf()
END

IF
	See("D5CAMBIO") 
THEN
	RESPONSE #100
		Attack("D5CAMBIO")
END
>>>>>>>>
COPY ~d5/d5mircam.baf~ ~weidu_external/reflections/compile/d5mircam.baf~
COMPILE ~weidu_external/reflections/compile/d5mircam.baf~

COPY_EXISTING ~idemon02.cre~ ~override/d5idem01.cre~
	WRITE_ASCII 0x248 ~D5MIRCAM~ #8
	WRITE_ASCII 0x280 ~D5MICAMB~ #20

COPY_EXISTING ~idemon02.cre~ ~override~
	WRITE_ASCII 0x248 ~D5IRECAM~ #8
	WRITE_ASCII 0x280 ~D5CAMBIO~ #20
	ADD_CRE_ITEM ~d5shard1~ #1 #0 #0 ~IDENTIFIED~ ~INV~

ACTION_IF (FILE_EXISTS_IN_GAME ~ar0602.bcs~) BEGIN

<<<<<<<< d5/d5ar602+.baf
IF
	Global("DemonWheel","AR0602",2)
THEN
	RESPONSE #100
		Wait(2)
		CreateCreature("D5IDEM01",[2525.1100],9)  // Mirror Fiend
		SetGlobal("DemonWheel","AR0602",3)
		Continue()
END
>>>>>>>>
EXTEND_TOP ~ar0602.bcs~ ~d5/d5ar602+.baf~

END	//	end ar0602

END	//	end if idemon02

//do stuff__________________________________________________________________________
//

