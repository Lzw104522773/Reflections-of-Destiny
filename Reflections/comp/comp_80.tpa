
DEFINE_ACTION_FUNCTION mirror_man BEGIN

/*

***** D5BDIMO won't perform her script
***** the cell dor is opening

- plan: jump from palace to FF cells, big cut scene w/ clone

 -- make cut scene and dialogue for Gregson to enter palace and say they have Charname in custody... er... wait
 
 -- make cut scene jumping party, gregson, belt, liia, and entar to jail, and walk to clone's cell, and dialogue
 
 -- make cut scene with old switcheroo, then more dialogue
 
 -- cut scene of everyone leaving
 
 -- cut scene with fade out/fade in, and imoen dialogue
 	...model this on the SoD scene
 	
 -- then tie into the escape sequence where you go through the cave and meet J/K, M/D

 -- use custom copy of FF HQ/basement

 -- use custom copy of sewers

// ***** check bdcut62 for cleaning up the jail area

*/

// prep journal entries ...?

//ADD_JOURNAL TITLE (@80001) @80002


// copy in stuff

COPY ~%MOD_FOLDER%/data/switcheroo/d50104.are~ ~override~
COPY ~%MOD_FOLDER%/data/switcheroo/d50105.are~ ~override~
COPY ~%MOD_FOLDER%/data/switcheroo/d56000.are~ ~override~
COPY ~%MOD_FOLDER%/data/switcheroo/d5bdimo.cre~ ~override~
COPY ~%MOD_FOLDER%/data/switcheroo/d5chrnam.cre~ ~override~
COPY ~%MOD_FOLDER%/data/switcheroo/d5bracs.itm~ ~override~
COPY ~%MOD_FOLDER%/data/switcheroo/d5_div.spl~ ~override~
COPY ~%MOD_FOLDER%/data/switcheroo/d5pause.spl~ ~override~
COPY ~%MOD_FOLDER%/data/switcheroo/d5pausx.spl~ ~override~

COPY ~%MOD_FOLDER%/data/switcheroo/d50104.baf~ ~weidu_external/%MOD_FOLDER%/compile/d50104.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/d50104.baf~
COPY ~%MOD_FOLDER%/data/switcheroo/d50105.baf~ ~weidu_external/%MOD_FOLDER%/compile/d50105.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/d50105.baf~
COPY ~%MOD_FOLDER%/data/switcheroo/d56000.baf~ ~weidu_external/%MOD_FOLDER%/compile/d56000.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/d56000.baf~ EVALUATE_BUFFER
COPY ~%MOD_FOLDER%/data/switcheroo/d5bdimo.baf~ ~weidu_external/%MOD_FOLDER%/compile/d5bdimo.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/d5bdimo.baf~
COPY ~%MOD_FOLDER%/data/switcheroo/d5cut64.baf~ ~weidu_external/%MOD_FOLDER%/compile/d5cut64.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/d5cut64.baf~


// gregson cut scene in palace 

<<<<<<<< d5/ar108+8.baf
IF
	Global("D5GregsonCut","GLOBAL",0)
	Global("D5GregsonPalace","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("D5GregsonCut","GLOBAL",1)
		CreateCreature("D5GREGS",[520.615],E)
		ClearAllActions()
		StartCutSceneMode()
		StartCutScene("D5GRCUT1")
END

IF
	Global("D5CloneCut","GLOBAL",1)
	Global("D5GregsonPalace","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("D5CloneCut","GLOBAL",2)
		ClearAllActions()
		StartCutSceneMode()
		StartCutScene("D5CLCUT1")
END
>>>>>>>>
COPY ~d5/ar108+8.baf~ ~weidu_external/%MOD_FOLDER%/compile/ar108+8.baf~
EXTEND_TOP ~%bg1_area_prefix%0108.BCS~ ~weidu_external/%MOD_FOLDER%/compile/ar108+8.baf~

// grcut1 = gregson run in and say charname is in custody

<<<<<<<< d5/d5grcut1.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId("D5GREGS")  // Captain Gregson
		DisplayStringHead(Myself,@80005)  // Lord Entar! You must come quickly! Oh, hello everyone...
		MoveToPoint([640.465])
		Wait(1)
		SetGlobal("D5CloneCut","GLOBAL",1)
		EndCutSceneMode()
		ActionOverride("D5GREGS",StartDialogueNoSet(Player1))
END
>>>>>>>>
COPY ~d5/d5grcut1.baf~ ~weidu_external/%MOD_FOLDER%/compile/d5grcut1.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/d5grcut1.baf~

// clcut1 = everyone leave the palace
 
<<<<<<<< d5/d5clcut1.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId(Player1)
		ActionOverride("D5GREGS",EscapeAreaObject("Door0200"))
		ActionOverride("BDENTAR",EscapeAreaObject("Door0200"))
		ActionOverride("BELT",EscapeAreaObject("Door0200"))
		ActionOverride("LIIA",EscapeAreaObject("Door0200"))
		Wait(1)
		MoveToPoint([460.715])
		FadeToColor([1.0],0)
		ActionOverride(Player2,LeaveAreaLUA("d50104","",[1350.975],W))
		ActionOverride(Player3,LeaveAreaLUA("d50104","",[1380.985],W))
		ActionOverride(Player4,LeaveAreaLUA("d50104","",[1410.995],W))
		ActionOverride(Player5,LeaveAreaLUA("d50104","",[1440.1005],W))
		ActionOverride(Player6,LeaveAreaLUA("d50104","",[1470.1015],W))
		LeaveAreaLUAPanic("d50104","",[805.760],S)
		LeaveAreaLUA("d50104","",[805.760],S)
		EndCutSceneMode()
END
>>>>>>>>
COPY ~d5/d5clcut1.baf~ ~weidu_external/%MOD_FOLDER%/compile/d5clcut1.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/d5clcut1.baf~

// clcut2 = arrive at FF jail, go to cell

<<<<<<<< d5/d5clcut2.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId(Player1)
		OpenDoor("door03")
		CreateCreatureCopyPoint("D5CHRNAM",Player1,[410.340])  // No such index
		CreateCreature("D5GREGS",[745.680],W)  // Captain Gregson
		CreateCreature("BDENTAR",[775.700],W)  // Entar Silvershield
		CreateCreature("BELT",[855.705],W)  // Belt
		CreateCreature("LIIA",[810.720],W)  // Liia Jannath
		GiveItemCreate("D5BRACS","D5CHRNAM",1,0,0)  // Bracers
		XEquipItem("D5BRACS","D5CHRNAM",SLOT_GAUNTLETS,EQUIP)  // Bracers
		FadeFromColor([1.0],0)
		MoveViewPoint([805.760],INSTANT)
		SmallWait(9)
		MoveViewPoint([330.470],VERY_FAST)
		ActionOverride("D5GREGS",MoveToPoint([315.425]))
		SmallWait(7)
		ActionOverride("BDENTAR",MoveToPoint([260.455]))
		SmallWait(5)
		ActionOverride("LIIA",MoveToPoint([345.445]))
		Wait(1)
		ActionOverride("BELT",MoveToPoint([380.460]))
		Wait(1)
		MoveToPoint([330.470])
		Wait(1)
		EndCutSceneMode()
		ActionOverride("BELT",StartDialogueNoSet(Player1))
END
>>>>>>>>
COPY ~d5/d5clcut2.baf~ ~weidu_external/%MOD_FOLDER%/compile/d5clcut2.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/d5clcut2.baf~

// clcut3 = liia casts a divination spell

<<<<<<<< d5/d5clcut3.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId(Player1)
		Wait(2)
		ActionOverride("LIIA",ForceSpellRES("D5_DIV","D5CHRNAM"))  // No such index
		Wait(2)
		CreateVisualEffectObject("SPSTRENH","D5CHRNAM")  // No such index
		Wait(2)
		EndCutSceneMode()
		ActionOverride("LIIA",StartDialogueNoSet(Player1))
END
>>>>>>>>
COPY ~d5/d5clcut3.baf~ ~weidu_external/%MOD_FOLDER%/compile/d5clcut3.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/d5clcut3.baf~

// clcut4 = timestop and switch places, followed by another liia divination

<<<<<<<< d5/d5clcut4.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId(Player1)
		Wait(2)
		AddAreaFlag(DREAMAREA)
		Wait(2)
		JumpToPoint([420.340])
		MoveViewPoint([420.340],INSTANT)
		ActionOverride("D5CHRNAM",JumpToPoint([320.485]))
		ActionOverride("Equipment_p1",TakeCreatureItems(Player1,ALL))
		Wait(2)
		ActionOverride(Player2,LeaveParty())
		ActionOverride(Player3,LeaveParty())
		ActionOverride(Player4,LeaveParty())
		ActionOverride(Player5,LeaveParty())
		ActionOverride(Player6,LeaveParty())
		Wait(1)
		RemoveAreaFlag(DREAMAREA)
		Wait(1)
		ActionOverride("LIIA",DisplayStringHead(Myself,@80035))  // Apologies, whoever you are, this might sting a bit.
		Wait(1)
		ActionOverride("LIIA",ForceSpellRES("D5_DIV",Player1))  // No such index
		Wait(2)
		CreateVisualEffectObject("ICGLYPTI",Myself)
		Wait(1)
		PlayDead(2)
		Wait(1)
		EndCutSceneMode()
		ActionOverride("LIIA",StartDialogueNoSet(Player1))
END
>>>>>>>>
COPY ~d5/d5clcut4.baf~ ~weidu_external/%MOD_FOLDER%/compile/d5clcut4.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/d5clcut4.baf~

// clcut5 = everyone leaves, you are stuck

<<<<<<<< d5/d5clcut5.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId(Player1)
		Wait(1)
		ActionOverride(Player2,EscapeAreaObject("TranBD0030"))
		ActionOverride(Player3,EscapeAreaObject("TranBD0030"))
		ActionOverride(Player4,EscapeAreaObject("TranBD0030"))
		ActionOverride(Player5,EscapeAreaObject("TranBD0030"))
		ActionOverride(Player6,EscapeAreaObject("TranBD0030"))
		ActionOverride("D5GREGS",EscapeAreaNoSee())
		ActionOverride("BDENTAR",EscapeAreaNoSee())
		ActionOverride("BELT",EscapeAreaNoSee())
		ActionOverride("LIIA",EscapeAreaNoSee())
		ActionOverride("D5CHRNAM",EscapeAreaNoSee())
		Wait(1)
		ActionOverride("witness",DestroySelf())
		ActionOverride("witness_guard",DestroySelf())
		ActionOverride("bdbence",DestroySelf())
		ActionOverride("bence_guard",DestroySelf())
		ActionOverride("bdeltan",DestroySelf())
		ActionOverride("Entar Guard 1",DestroySelf())
		ActionOverride("Entar Guard 2",DestroySelf())
		PlaySound("AMBDLBOO")
		Wait(2)
		MultiPlayerSync()
		Wait(1)
		CloseDoor("door01")
		CloseDoor("door02")
		CloseDoor("door07")
		CloseDoor("door08")
		Lock("door02")
		Lock("door07")
		Lock("door08")
		TriggerActivation("InfoBDStairs",FALSE)
		TriggerActivation("Infobdstairs_epilogue",TRUE)
		TriggerActivation("TrD50105",TRUE)
		AddAreaType(CANRESTOTHER)
		ReallyForceSpellRES("bdremove",Myself)  // No such index
		SmallWait(5)
		CreateCreature("bdrefuge",[344.619],NE)  // Refugee
		CreateCreature("bdrefgm2",[472.677],NE)  // Refugee
		CreateCreature("bdclara",[600.738],NE)  // Clara
		CreateCreature("bdrefgf1",[648.474],SW)  // Refugee
		CreateCreature("bdrefgf2",[762.545],SW)  // Refugee
		SetGlobal("bd_plot","global",605)
		SetGlobal("D5CloneCut","GLOBAL",10)
		EndCutSceneMode()
END
>>>>>>>>
COPY ~d5/d5clcut5.baf~ ~weidu_external/%MOD_FOLDER%/compile/d5clcut5.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/d5clcut5.baf~

// imoen in sewer cave

<<<<<<<< d5/d5clcut6.baf
IF
	True()
THEN
	RESPONSE #100
		CutSceneId(Player1)
		SmallWait(9)
		ActionOverride("D5BDIMO",ForceSpellRES("SPWI206",Myself))  // Invisibility
		Wait(1)
		SetGlobal("bd_plot","global",641)
		ActionOverride("D5BDIMO",EscapeAreaNoSee())
		Wait(1)
		EndCutSceneMode()
END
>>>>>>>>
COPY ~d5/d5clcut6.baf~ ~weidu_external/%MOD_FOLDER%/compile/d5clcut6.baf~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/d5clcut6.baf~


// dialogues

<<<<<<<< d5/switch+.d
ADD_STATE_TRIGGER BELT 0 ~Global("D5CloneCut","GLOBAL",0)~

APPEND BELT

IF ~GlobalGT("D5GregsonCut","GLOBAL",0) Global("D5CloneCut","GLOBAL",0)~ BEGIN d5beltsw_0
  SAY @80007
  IF ~~ EXTERN D5GREGS d5gregsw_2
END

IF ~~ BEGIN d5beltsw_1
  SAY @80011
  IF ~~ EXTERN BDENTAR d5entarsw_1
END

IF ~~ BEGIN d5beltsw_2
  SAY @80014
  IF ~~ REPLY @80015 DO ~SetGlobal("D5CloneCut","GLOBAL",1)~ EXIT
END

/*
IF ~~ BEGIN d5beltsw_3
  SAY @80018
  IF ~~ REPLY @80019 DO ~SetGlobal("D5CloneCut","GLOBAL",1)~ EXIT
  IF ~~ REPLY @80020 DO ~SetGlobal("D5AllEnemy","GLOBAL",1)~ EXIT
END
*/

IF ~Global("D5CloneCut","GLOBAL",3)~ BEGIN d5beltsw_4
  SAY @80024
  IF ~~ EXTERN BDENTAR d5entarsw_2
END

IF ~~ BEGIN d5beltsw_5
  SAY @80028
  IF ~~ EXTERN LIIA d5liiasw_1
END

IF ~~ BEGIN d5beltsw_6
  SAY @80030
  IF ~~ DO ~SetGlobal("D5CloneCut","GLOBAL",4)~ EXIT
END

IF ~Global("D5CloneCut","GLOBAL",7)~ BEGIN d5beltsw_7
  SAY @80037
  IF ~~ EXTERN D5CHRNAM d5chrnam_1
END

IF ~~ BEGIN d5beltsw_8
  SAY @80040
  IF ~~ DO ~SetGlobal("D5CloneCut","GLOBAL",8)~ EXIT
END

END

APPEND BDENTAR

IF ~Global("D5GregsonCut","GLOBAL",1)~ BEGIN d5entarsw_0
  SAY @80009
  IF ~~ EXTERN D5GREGS d5gregsw_3
END

IF ~~ BEGIN d5entarsw_1
  SAY @80012
  IF ~~ EXTERN LIIA d5liiasw_0
END

IF ~Global("D5CloneCut","GLOBAL",3)~ BEGIN d5entarsw_2
  SAY @80025
  IF ~~ REPLY @80026 EXTERN D5CHRNAM d5chrnam_0
END

END

APPEND LIIA

IF ~Global("D5GregsonCut","GLOBAL",1) GlobalLT("D5CloneCut","GLOBAL",3)~ BEGIN d5liiasw_0
  SAY @80013
  IF ~~ EXTERN BELT d5beltsw_2
END

IF ~Global("D5CloneCut","GLOBAL",3)~ BEGIN d5liiasw_1
  SAY @80029
  IF ~~ EXTERN BELT d5beltsw_6
END

IF ~Global("D5CloneCut","GLOBAL",5)~ BEGIN d5liiasw_2
  SAY @80031
  IF ~Global("D5heritageRevealed","GLOBAL",0)~ REPLY @80032 GOTO d5liiasw_3
  IF ~Global("D5heritageRevealed","GLOBAL",1)~ REPLY @80033 GOTO d5liiasw_3
END

IF ~~ BEGIN d5liiasw_3
  SAY @80034
  IF ~~ DO ~SetGlobal("D5CloneCut","GLOBAL",6)~ EXIT
END

IF ~Global("D5CloneCut","GLOBAL",7)~ BEGIN d5liiasw_4
  SAY @80036
  IF ~~ EXTERN BELT d5beltsw_7
END

END

APPEND D5GREGS

IF ~Global("D5GregsonCut","GLOBAL",1)~ BEGIN d5gregsw_0
  SAY @80005
  IF ~~ GOTO d5gregsw_1
END

IF ~~ BEGIN d5gregsw_1
  SAY @80006
  IF ~~ EXTERN BELT d5beltsw_0
END

IF ~~ BEGIN d5gregsw_2
  SAY @80008
  IF ~~ EXTERN BDENTAR d5entarsw_0
END

IF ~~ BEGIN d5gregsw_3
  SAY @80010
  IF ~~ EXTERN BELT d5beltsw_1
END

END

BEGIN ~D5CHRNAM~

IF ~~ BEGIN d5chrnam_0
  SAY @80027
  IF ~~ EXTERN BELT d5beltsw_5
END

IF ~Global("D5CloneCut","GLOBAL",7)~ BEGIN d5chrnam_1
  SAY @80038
  IF ~~ REPLY @80039 EXTERN BELT d5beltsw_8
END

BEGIN ~D5BDIMO~

IF ~Global("D5CloneCut","GLOBAL",11)~ BEGIN d5imoensw_0
  SAY @80050
  IF ~Alignment(Player1,MASK_GOOD)~ REPLY @80051 GOTO d5imoensw_1
  IF ~Alignment(Player1,MASK_GENEUTRAL)~ REPLY @80051 GOTO d5imoensw_1
  IF ~Alignment(Player1,MASK_EVIL)~ REPLY @80051 GOTO d5imoensw_2
END

IF ~~ BEGIN d5imoensw_1
  SAY @80052
  IF ~~ REPLY @80054 GOTO d5imoensw_3
END

IF ~~ BEGIN d5imoensw_2
  SAY @80053
  IF ~~ REPLY @80055 GOTO d5imoensw_3
END

IF ~~ BEGIN d5imoensw_3
  SAY @80056
  IF ~~ REPLY @80057 GOTO d5imoensw_4
END

IF ~~ BEGIN d5imoensw_4
  SAY @80058
  IF ~~ REPLY @80059 GOTO d5imoensw_5
END

IF ~~ BEGIN d5imoensw_5
  SAY @80060
  IF ~~ GOTO d5imoensw_6
END

IF ~~ BEGIN d5imoensw_6
  SAY @80061
  IF ~~ REPLY @80062 GOTO d5imoensw_7
END

IF ~~ BEGIN d5imoensw_7
  SAY @80063
  IF ~~ DO ~OpenDoor("Door04") SetGlobal("D5CloneCut","GLOBAL",12)~ EXIT
END

IF ~Global("D5CloneCut","GLOBAL",12) AreaCheck("d50104")~ BEGIN d5imoensw_8
  SAY @80064
  IF ~~ EXIT
END

IF ~GlobalLT("bd_plot","global",625) Global("D5CloneCut","GLOBAL",12) AreaCheck("d50105")~ BEGIN d5imoensw_9
  SAY @80065
  IF ~~ EXIT
END

IF ~GlobalLT("bd_plot","global",641) Global("D5CloneCut","GLOBAL",12) AreaCheck("d56000")~ BEGIN d5imoensw_10
  SAY @80066
  IF ~~ DO ~SetGlobal("D5ImoenCaveCut","GLOBAL",1)~ EXIT
END
>>>>>>>>
COPY ~d5/switch+.d~ ~weidu_external/%MOD_FOLDER%/compile/switch+.d~
COMPILE ~weidu_external/%MOD_FOLDER%/compile/switch+.d~

//___________________________________________________________________________________
//


END	//	end define function