
DEFINE_ACTION_FUNCTION kill_thalantyr BEGIN

/*
- set area script

- ...chapter check?
- patch area script for thalantyr's interior
- remove thalantyr and set GV
- patch melicamp's dialogue in response to the GV
 -- "You'll have to excuse me, everything is a mess, with Thalantyr gone. I am managing things until his return."
 -- [cond1] where has he gone?
 -- [cond2] would you be willing to buy or sell items from thalantyr's stock?
 -- "Oh, he has not gone anywhere. He is dead! Killed. I have no idea who did it. 
 -- [if recovered] And whoever it was, they took those thrice-damned Netherese bracers!" 
 -- that sounds bad. but why did you say when he returns? is he to be resurrected?
 -- "Oh no, no, he does not approve of the use of clerical magic. He has made his own contingent arrangements in case of his demise. Some kind of combination of reincarnation and regeneration, if I understand it correctly. Should be complete in a few... weeks? Maybe? I'm afraid it's a bit over my head - I'm just his apprentice! ...I have really learned to appreciate my limits, lately."
 -- would you be willing to buy or sell items from thalantyr's stock?
 -- "Ah. Well, I suppose he would not mind if I conducted some business on his behalf. He has trained me well in balancing the tower's accounts, if not in the actual use of magic."
 
- check for The Calling; disable the bit where you take & destroy the bracers
*/


//___________________________________________________________________________________
//

<<<<<<<< d5/area3202.baf
IF
	Global("thalantyr_global","GLOBAL",0)
THEN
	RESPONSE #100
		ActionOverride("thalantyr",MakeGlobal())
		SetGlobal("thalantyr_global","GLOBAL",1)
END
// ...is that ^ necessary?

IF
	GlobalGT("CHAPTER","GLOBAL",6)
	Global("thalantyr_dead","GLOBAL",0)
THEN
	RESPONSE #100
		ActionOverride("thalantyr",DestroySelf())		
		SetGlobal("thalantyr_dead","GLOBAL",1)
END
>>>>>>>>
COPY ~d5/area3202.baf~ ~weidu_external/reflections/compile/%bg1_area_prefix%3202.baf~

ACTION_IF (FILE_EXISTS_IN_GAME ~%bg1_area_prefix%3202.bcs~) BEGIN
  EXTEND_TOP ~%bg1_area_prefix%3202.BCS~ ~weidu_external/reflections/compile/%bg1_area_prefix%3202.baf~
END

ACTION_IF !(FILE_EXISTS_IN_GAME ~%bg1_area_prefix%3202.bcs~) BEGIN
  COMPILE ~weidu_external/reflections/compile/%bg1_area_prefix%3202.baf~
END

// ***** add dialogue options to melicamp if Global("thalantyr_dead","GLOBAL",1) (melica.dlg)

<<<<<<<< d5/melica+.d
REPLACE_STATE_TRIGGER MELICA 26 ~Global("thalantyr_dead","GLOBAL",0)~

APPEND MELICA

IF ~GlobalGT("thalantyr_dead","GLOBAL",1)~ THEN BEGIN d5melica_0 
  SAY @20001
  IF ~Global("thalantyr_dead","GLOBAL",1)~ THEN REPLY @20002 DO ~SetGlobal("thalantyr_dead","GLOBAL",2)~ GOTO d5melica_1
  IF ~Global("thalantyr_dead","GLOBAL",2)~ THEN REPLY @20007 GOTO d5melica_4
  IF ~Global("thalantyr_dead","GLOBAL",2)~ THEN REPLY @20010 EXIT
END

IF ~~ THEN BEGIN d5melica_1
  SAY @20003
  IF ~GlobalGT("CDBracerQuest","GLOBAL",18)~ THEN REPLY ~~ GOTO d5melica_2
  IF ~GlobalLT("CDBracerQuest","GLOBAL",19)~ THEN REPLY @20005 GOTO d5melica_3
END

IF ~~ THEN BEGIN d5melica_2
  SAY @20004
  IF ~~ THEN REPLY @20005 GOTO d5melica_3
END

IF ~~ THEN BEGIN d5melica_3
  SAY @20006
  IF ~~ THEN REPLY @20007 GOTO d5melica_4
  IF ~~ THEN REPLY @20010 EXIT
END

IF ~~ THEN BEGIN d5melica_4
  SAY @20008
  IF ~~ THEN REPLY @20009 DO ~StartStore("highhedg",LastTalkedToBy(Myself))~ EXIT
END

END
>>>>>>>> 
COPY ~d5/melica+.d~ ~weidu_external/reflections/compile/melica+.d~
COMPILE ~weidu_external/reflections/compile/melica+.d~

ACTION_IF (MOD_IS_INSTALLED ~thecalling.tp2~ ~0~) BEGIN

// ***** forestall the last bit of The Calling

// find STATE_WHICH_SAYS "That may *cluck* take a bit longer to pass" in melica.dlg, and add (EXTEND_BOTTOM) a new transition sending it to a NEW state added to thalan.dlg

OUTER_SET melica_thecalling_1285 = STATE_WHICH_SAYS 1285 IN ~thecalling/languages/%s/mage.tra~ FROM ~MELICA~

PRINT ~the state we want is # %melica_thecalling_1285%~

<<<<<<<< d5/thalan+.d
APPEND THALAN

IF ~~ THEN BEGIN d5thalan_0 
  SAY @20011
  IF ~~ THEN REPLY @20012 EXIT
  IF ~~ THEN REPLY @20013 DO ~StartStore("highhedg",LastTalkedToBy(Myself))~ EXIT
  IF ~~ THEN REPLY @20014 EXIT
END

END
>>>>>>>>
COPY ~d5/thalan+.d~ ~weidu_external/reflections/compile/thalan+.d~
COMPILE ~weidu_external/reflections/compile/thalan+.d~

OUTER_SET thalan_20011 = STATE_WHICH_SAYS @20011 FROM ~THALAN~

<<<<<<<< d5/melica#.d
EXTEND_BOTTOM MELICA %melica_thecalling_1285%

IF ~~ THEN REPLY ~Wotta relief!~ EXTERN ~THALAN~ %thalan_20011% 

END
>>>>>>>>
COPY ~d5/melica#.d~ ~weidu_external/reflections/compile/melica#.d~
COMPILE EVAL ~weidu_external/reflections/compile/melica#.d~

// ***** also need to 

// ***** should we make sure the bracers don't get lost...??

END	//	end thecalling is installed

END	//	end define function